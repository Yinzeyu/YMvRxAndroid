buildscript {
    ext.kotlin_version = '1.4.10'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.3.1"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url "https://dl.bintray.com/thelasterstar/maven/" }
        maven { url 'https://dl.bintray.com/umsdk/release' }
        maven {url  "https://dl.bintray.com/rongcloud/maven"}
    }
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

//★修改version后需要将versionCode重置为从1开始★
def version_Name = "1.0.0"//只能是X.X.X格式，X为数字
//build版本从1开始：偶数测试版、奇数正式版
def version_Build = 1//只能是1-999的数字(预想999次build应该可以发版)
//默认显示的version_Code信息为:三位之前表示版本号、后三位表示当前版本的build次数
def version_Code = (version_Name.replace(".", "")).toInteger() * 1000 + version_Build

ext {
    android = [
            applicationId          : "com.yzy.example",
            compileSdkVersion      : 30,
            minSdkVersion          : 21,
            targetSdkVersion       : 30,
            versionName            : version_Name,
            versionCode            : version_Code,
            //渠道信息详见APP下Channel_Info.txt文件
            buildTime              : new Date().format("yyyyMMdd"), //编译日期
            computer               : Math.abs((System.getProperty("user.name") + "@"
                    + InetAddress.localHost.hostName).hashCode()),
            javaSourceCompatibility: JavaVersion.VERSION_1_8,
            javaTargetCompatibility: JavaVersion.VERSION_1_8
    ]
}
